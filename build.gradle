plugins {
	id 'java' // Needed for assemble and build
	id 'maven-publish' // Provides publishing and publishToMavenLocal
	id 'com.enonic.defaults' version '2.0.1' // Publishing
	id 'com.enonic.xp.app' version '3.0.0'

	//id 'com.moowork.node' version '1.3.1'
	id 'com.github.node-gradle.node' version '3.1.1'
}


repositories {
	xp.enonicRepo('dev')
	//mavenLocal()
	mavenCentral()
	//jcenter()
}


app {
	name = project.appName
	group = 'com.enonic.app'
	displayName = 'Explorer'
	vendorName = 'Enonic AS'
	vendorUrl = 'http://enonic.com'
	systemVersion = "${xpVersion}"
}


dependencies {
	compile "com.enonic.xp:core-api:${xpVersion}"
	compile "com.enonic.xp:portal-api:${xpVersion}"

	//──────────────────────────────────────────────────────────────────────────
	// Core libs (com.enonic.xp)
	//──────────────────────────────────────────────────────────────────────────

	include "com.enonic.xp:lib-admin:${xpVersion}"
	include "com.enonic.xp:lib-auth:${xpVersion}"
	include "com.enonic.xp:lib-cluster:${xpVersion}"
	include "com.enonic.xp:lib-common:${xpVersion}"
	include "com.enonic.xp:lib-context:${xpVersion}" // Needed by lib-explorer
	include "com.enonic.xp:lib-event:${xpVersion}"
	include "com.enonic.xp:lib-i18n:${xpVersion}" // Needed by lib-explorer
	include "com.enonic.xp:lib-io:${xpVersion}"
	include "com.enonic.xp:lib-mail:${xpVersion}" // Needed by lib-explorer
	include "com.enonic.xp:lib-node:${xpVersion}" // Needed by lib-explorer
	include "com.enonic.xp:lib-portal:${xpVersion}"
	include "com.enonic.xp:lib-repo:${xpVersion}"
	include "com.enonic.xp:lib-scheduler:${xpVersion}"
	include "com.enonic.xp:lib-task:${xpVersion}"
	//include "com.enonic.xp:lib-thymeleaf:${xpVersion}"
	include "com.enonic.xp:lib-websocket:${xpVersion}"

	//──────────────────────────────────────────────────────────────────────────
	// Other enonic libs (com.enonic.lib)
	//──────────────────────────────────────────────────────────────────────────

	//include 'com.enonic.lib:lib-admin-ui:3.0.0' // Not needed for the XP Menu

	include 'com.enonic.lib:lib-cache:2.1.1'
	include 'com.enonic.lib:lib-graphql:2.0.0'
	//include 'com.enonic.lib:lib-graphql-playground:0.0.1'
	include 'com.enonic.lib:lib-guillotine:4.2.0'

	include 'com.enonic.lib:lib-http-client:2.3.0' // User-Agent: okhttp/4.9.1

	include 'com.enonic.lib:lib-license:3.0.0'
	include 'com.enonic.lib:lib-router:2.1.0'

	include "com.enonic.lib:lib-explorer:${libExplorerVersion}"

	include 'com.enonic.lib:lib-util:3.0.0'

	include 'com.enonic.lib:lib-galimatias:1.0.0-B1'

	// WARNING: Do NOT use include files, jar file will be missing dependencies!
}


tasks.withType(Copy) {
  includeEmptyDirs = false
}


//──────────────────────────────────────────────────────────────────────────────
// Gradle wrapper
//──────────────────────────────────────────────────────────────────────────────
wrapper {
	distributionUrl = "${gradleDistributionUrl}"
}


//──────────────────────────────────────────────────────────────────────────────
// Gradle node plugin
//──────────────────────────────────────────────────────────────────────────────
node {
	download = true
	version = "${nodeVersion}"
}


//──────────────────────────────────────────────────────────────────────────────
// Webpack
//──────────────────────────────────────────────────────────────────────────────
sourceSets {
	main {
		java {
			srcDir 'src/main/java'
		}
		resources { // Paths relative to src/main/resources
			exclude 'assets/**/*.svg'
		}
	}
}


task webpack(type:NodeTask) {
	dependsOn yarn_install
	mustRunAfter yarn_install
	script = file('node_modules/webpack-cli/bin/cli.js')
	args = [
		'--color'
	]
	outputs.dir './build/resources/main' // processResources will delete the directory if this is not present
}


task watch(type:NodeTask) {
	script = file('node_modules/webpack-cli/bin/cli.js')
	args = [
		'--color',
		'--watch'
	]
}


processResources {
	dependsOn webpack
	mustRunAfter webpack

	exclude '**/.gitkeep'

	// Let webpack handle these (rather than gradle)
	exclude '**/*.css'
	exclude '**/*.es'
	exclude '**/*.js'
	exclude '**/*.jsx'
	exclude '**/*.less'
	exclude '**/*.sass'
	exclude '**/*.scss'
	exclude '**/*.styl'
}

jar.dependsOn webpack
